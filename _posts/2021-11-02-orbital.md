---
title: "楕円軌道"
categories:
  - VBScript
tags:
  - VBScript
  - HTA
---


惑星の楕円軌道を表示するプログラムを作成した。HTAファイル上でCanvasオブジェクトを使ってアニメーションを描画する。





【概要】

ケプラーの第一法則

惑星は太陽を焦点の一つとする楕円軌道上を動く。

ケプラーの第二法則

太陽と惑星を結ぶ線分が単位時間に描く面積(面積速度)は
一定である。

上記法則をシミュレーションするプログラムである。

下記のHTAファイルを実行すると、最初に楕円の「離心率」を聞いてくるので、0～1の範囲で数値を入力する。0だと円運動になる。

画面中心の赤い丸は太陽を表し、青い丸は惑星を表す。
（まあ準惑星や小惑星や彗星と考えてくれてもよいです）

惑星が画面の右に来た時が太陽と惑星が最も近くなる「近日点」、左に来た時が太陽と惑星が最も遠くなる「遠日点」になる。上記の第二法則により、惑星の運動は、太陽から近いときほど速く（近日点において最速）、太陽から遠いときほど遅くなる（遠日点において最遅）。



ファイル名：DaenKidou.hta

```html
<!DOCTYPE html>
<html>
  <head>
    <meta http-equiv="X-UA-Compatible" content="IE=10">
    <title>惑星の楕円軌道</title>
    <script type="text/vbscript" src="daenkidou.txt">
    </script>
    <style type="text/css">
      canvas { background-color:#000; border: 1px solid #999; }
    </style>
  </head>
  <body onload="init()">
    <canvas id="tentai1" width="400" height="300"></canvas>
  </body>
</html>
```

ファイル名：DaenKidou.txt

```vb
Option Explicit
Dim ctx ' キャンバスオブジェクト
Dim aDaen ' 楕円軌道を格納する配列
Dim locM ' 配列の第1添字(0～359)
' ----------------------------------------------------------
' bodyロード時に実行される
' ----------------------------------------------------------
Function init()
  ' キャンバスオブジェクトを取得
  Set ctx = tentai1.getContext("2d")
  ' インプットボックスで離心率を入力
  Dim e
  e = InputBox("離心率を入力して下さい(0～1)", , 0.3)
  If Not IsNumeric(e) Then
    MsgBox "数字でないので終了します"
    window.close()
  End If
  ' 360行2列の配列にx,y座標を格納する
  aDaen = DaenKidou(e)
  ' アニメーション
  locM = 0
  Window.setinterval "draw()", 20 ' 20ミリ秒ごとに描画実行
End Function
' ----------------------------------------------------------
' 描画実行
' ----------------------------------------------------------
Function draw()
  ' 画面を塗りつぶし
  ctx.globalCompositeOperation = "source-over"
  ctx.fillStyle = "rgba(6,6,6,0.5)" '
  ctx.fillRect 0, 0, 400, 300
  ' 太陽を表す赤い円を描く
  Call ctx.beginPath
  ctx.fillStyle = "#FF6633"
  ctx.Arc 200, 150, 4, 0, Pi() * 2, True
  Call ctx.Fill
 
  Dim locX
  Dim locY
  ' 軌道を表す灰色の円を描く
  Dim i
  For i = 0 To 35
    If i * 10 > locM Then Exit For
    locX = Int(200 + aDaen(i * 10, 0) * 100)
    locY = Int(150 - aDaen(i * 10, 1) * 100)
    Call ctx.beginPath
    ctx.fillStyle = "#666666"
    ctx.Arc locX, locY, 4, 0, Pi() * 2, True
    Call ctx.Fill
  Next
  ' 惑星の位置を更新
  locM = locM + 1
  If locM > 359 Then locM = 0
  locX = Int(200 + aDaen(locM, 0) * 100)
  locY = Int(150 - aDaen(locM, 1) * 100)
  ' 更新した座標で青い円を描く
  Call ctx.beginPath
  ctx.globalCompositeOperation = "lighter"
  ctx.fillStyle = "#3399FF"
  ctx.Arc locX, locY, 4, 0, Pi() * 2, True
  Call ctx.Fill
End Function
' ----------------------------------------------------------
' 楕円軌道
' ----------------------------------------------------------
Function DaenKidou(e)
  ' e 離心率(0～1)
 
  ' 平均近点離角を0～359度まで1度ずつ変えた時の
  ' x,y座標を格納する360行2列の配列
  Dim aPlot(359, 1)
  ' 楕円軌道の長半径を1としたときの短半径
  Dim b
  b = Sqr(1 - e ^ 2)
 
  Dim M ' 平均近点離角[度]
  Dim U ' 離心近点離角[rad]
  For M = 0 To 359
    ' Mを度からRadへ単位変換して、Uを取得
    U = Kepler(e, M / 180 * Pi())
    ' x座標、焦点(太陽)を中心にするためeを引く
    aPlot(M, 0) = Cos(U) - e
    ' y座標
    aPlot(M, 1) = b * Sin(U)
  Next
  ' 配列を返す
  DaenKidou = aPlot
End Function
' ----------------------------------------------------------
' 離心率と平均近点離角から離心近点離角を求める
' ----------------------------------------------------------
Function Kepler(e, M)
  ' e 離心率(0～1)
  ' M 平均近点離角[rad]
 
  ' ケプラー方程式「U-M=e*sin(U)」を
  ' U-M=aとおいて漸近法で解く
  Dim a1, a2
  a1 = 0
  a2 = e * Sin(a1 + M)
  Do While Abs(a2 - a1) > 0.00001
    a1 = a2
    a2 = e * Sin(a1 + M)
  Loop
  ' 値U(=a+M)を返す
  Kepler = a2 + M
End Function
' ----------------------------------------------------------
' 円周率
' ----------------------------------------------------------
Function Pi()
  Pi = 4 * Atn(1)
End Function
```

Canvasを使うにあたり参考にしたサイト：

[http://yoppa.org/taumedia10/2065.html](http://yoppa.org/taumedia10/2065.html)


【解説】

HTAファイル内の
`<body onload="init()">`
により、まず init 関数が呼び出される。

init 関数内で
`Set ctx = tentai1.getContext("2d")`
によりCanvasオブジェクトを取得。

`aDaen = DaenKidou(e)`

離心率eを引数として DaenKidou 関数を呼び出す。DaenKidou 関数では、平均近点離角0～359度の1度ずつに対応して惑星のx,y座標を計算し、360行2列の配列が変数 aDaenに格納される。

DaenKidou 関数内では
`U = Kepler(e, M / 180 * Pi())`
により Kepler 関数を呼び出して、離心率eと平均近点離角Mから離心近点離角を計算し、変数 U に格納する。Kepler 関数がこのプログラムの主役と言える。

Kepler 関数では、ケプラー方程式「U-M=e*sin(U)」を数値計算で解いている。
ケプラー方程式が何なのかは、大学物理の「力学」の教科書か演習書の、惑星の運動を扱っているページで解説されてると思う。

init 関数内の
`Window.setinterval "draw()", 20`
により20ミリ秒ごとに draw 関数を呼び出し、描画を繰り返す。
setinterval 関数はVBScriptの機能でない。ブラウザが提供している機能と思います。

draw関数内では

```vb
ctx.fillStyle = "rgba(6,6,6,0.5)" '
ctx.fillRect 0, 0, 400, 300
```
により、塗りつぶしの色と透明度を指定し、横400、縦300の画面全体を四角く塗りつぶす。

円を描くには

```vb
Call ctx.beginPath
ctx.fillStyle = "#FF6633"
ctx.Arc 200, 150, 4, 0, Pi() * 2, True
Call ctx.Fill
```

経路を開始、色を指定、扇形を指定するArcにて角度0～2*Piとして円にして、Fillで塗りつぶす。

配列 aDaen に格納されたx,y座標をCanvas上の座標へ変換する。Canvasの大きさはここでは横400、縦300なので中心の座標は200,150。Canvasでは左上端が0,0なので以下のようにy座標にマイナスがかかる。楕円の長半径1をCanvas上の長さ100とするため係数100をかけた。

```vb
locX = Int(200 + aDaen(locM, 0) * 100)
locY = Int(150 - aDaen(locM, 1) * 100)
```

配列 aDaen に格納されたx,y座標を1行ずつずらしながら青い円を描いて惑星の運動を表現し、10行ずつの値を灰色で描いて惑星が動いた跡を表現した。
