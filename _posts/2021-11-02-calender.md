---
layout: single
title: "万年カレンダー"
categories: VBScript
---

修正ユリウス日-4を7で割った余りが曜日番号

（0：日、1：月、2：火、3：水、4：木、5：金、6：土）

になることを利用してカレンダーを表示するプログラムを作成した。

メッセージボックスにカレンダーを表示する。

- 紀元前x年は西暦-(x-1)年で表している。

- ユリウス暦でのうるう年は単純に4年ごととしている。

- ユリウス暦からグレゴリオ暦へ切り替わる1582年10月は
4日（ユリウス暦）の翌日が15日（グレゴリオ暦）。


### 関数の説明

Main：プログラム本体。

GetCalender：カレンダーを配列として取得。

MJD、Julian：日付から修正ユリウス日を算出。

GDate、GetDate、CalcDate：修正ユリウス日から日付を算出。

MonthDay：2月末日からM月末日までの日数を取得。

ファイル名：Calender.vbs

```vb
Call Main

' ----------------------------------------------------------
' メイン
' ----------------------------------------------------------
Function Main()
  ' 年月を入力
  Dim YY, MM
  YY = InputBox("年を入力して下さい", , Year(Date))
  MM = InputBox("月を入力して下さい", , Month(Date))
  ' カレンダーを配列で取得
  Dim aCalender
  aCalender = GetCalender(YY, MM)
  ' 出力用の文字列
  Dim sCalender
  sCalender = "  日   月  火  水   木  金  土" & vbCrLf
  ' 配列を文字列に変換
  Dim i, j
  For i = 0 To 5
    For j = 0 To 6
      sCalender = sCalender & String(3 - Len(aCalender(i, j)), "_") & aCalender(i, j)
    Next
    sCalender = sCalender & vbCrLf ' 週ごとに改行
  Next
  ' 結果を表示
  MsgBox sCalender, , YY & "年" & MM & "月"
End Function

' ----------------------------------------------------------
' 万年カレンダー
' カレンダーの配列を返す
' ----------------------------------------------------------
Function GetCalender(YY, MM)

  ' 当月1日と翌月1日の修正ユリウス日
  Dim JD1, JD2
  JD1 = MJD(YY, MM, 1)
  JD2 = MJD(YY, MM + 1, 1)
  ' カレンダーの配列
  Dim arrCalender(5, 6)
  Dim i ' 週
  Dim j ' 曜日: 0(日)～6(土)
  j = (JD1 - 4) - Int((JD1 - 4) / 7) * 7 ' 当月1日の曜日
  i = 0
  Do While JD1 < JD2
    arrCalender(i, j) = GDate(JD1)(2) ' 日付
    JD1 = JD1 + 1
    j = j + 1
    If j > 6 Then
      j = 0
      i = i + 1
    End If
  Loop
  ' 結果を返す
  GetCalender = arrCalender
End Function

' ----------------------------------------------------------
' 修正ユリウス日を返す
' 1582年10月15日以降はグレゴリオ暦の日付とみなす
' それより前はユリウス暦の日付とみなす
' ----------------------------------------------------------
Function MJD(ByVal Y, ByVal M, ByVal D)
  Dim P5, JD
  ' 年月日を8桁の数字に変換
  P5 = 10000 * Y + 100 * M + D
  If P5 < 15821015 Then
    JD = Julian(Y, M, D, 1) ' ユリウス暦
  Else
    JD = Julian(Y, M, D, 2) ' グレゴリオ暦
  End If
  ' 結果を返す
  MJD = JD
End Function

' ----------------------------------------------------------
 ' 修正ユリウス日を算出
' ----------------------------------------------------------
Function Julian(ByVal Y, ByVal M, ByVal D, intCalender)
' intCalender:暦を指定 (1:ユリウス暦, 2:グレゴリオ暦)
  Dim JD

  ' 1,2月を前年の13,14月にする
  If M < 3 Then
     Y = Y - 1
     M = M + 12
  End If
  ' 暦に応じて日付から修正ユリウス日を算出
  Select Case intCalender
   Case 1
     JD = -678884 + 365 * Y + MonthDay(M - 1) + D + Int(Y / 4)
   Case 2
     JD = -678882 + 365 * Y + MonthDay(M - 1) + D + Int(Y / 4) - Int(Y / 100) + Int(Y / 400)
  End Select
  ' 結果を返す
  Julian = JD
End Function

' ----------------------------------------------------------
' 修正ユリウス日から日付を算出
' 1582年10月15日以降はグレゴリオ暦の日付を返す
' それより前はユリウス暦の日付を返す
' ----------------------------------------------------------
Function GDate(JD)
  If JD < -100840 Then
     GDate = GetDate(JD, 1) ' ユリウス暦
  Else
     GDate = GetDate(JD, 2) ' グレゴリオ暦
  End If
End Function

' ----------------------------------------------------------
 ' 修正ユリウス日から日付を算出
' ----------------------------------------------------------
Function GetDate(JD, intCalender)
' intCalender:暦を指定 (1:ユリウス暦, 2:グレゴリオ暦)
  Dim Y, M, D, RD
  ' 紀元前1年2月29日を第0日とした日数を求める
  Select Case intCalender
  Case 1
    RD = JD + 678884
  Case 2
    RD = JD + 678882
  End Select
  Y = 0
  M = 2
  D = 29
  ' RDが0になるまで繰り返し処理
  Do Until RD = 0
    Call CalcDate(Y, M, D, RD, intCalender) ' 変数は参照渡し
  Loop
  GetDate = Array(Y, M, D)
End Function

' ----------------------------------------------------------
' GetDateのサブルーチン
' RDを年月日へ変換していく
' ----------------------------------------------------------
Function CalcDate(Y, M, D, RD, intCalender)
  If Abs(RD) < 1461 Then ' RDが±4年未満の場合
    If RD > 0 Then ' >0の場合
      If RD < 365 Then
        ' 1年未満の場合、月日を確定
        M = 3
        Do While RD > MonthDay(M)
          M = M + 1
        Loop
        D = RD - MonthDay(M - 1)
        RD = 0
        If M > 12 Then
          Y = Y + 1
          M = M - 12
        End If
      Else
        ' 1年以上の場合
        If Int(RD / 365) = RD / 365 Then
          ' 2月末日の場合
          Y = Y + RD / 365
          M = 2
          D = 28
          RD = 0
        Else
          ' 2月末日以外
          Y = Y + Int(RD / 365)
          RD = RD - Int(RD / 365) * 365
        End If
      End If
    Else ' <0の場合
      Y = Y - 4
      RD = RD + 1461
    End If
  Else
    If Abs(RD) < 36524 Or intCalender = 1 Then
      ' ±100年未満の場合、またはユリウス暦の場合
      If Int(RD / 1461) = RD / 1461 Then
        ' 2月末日の場合
        Y = Y + RD / 1461 * 4
        M = 2
        D = 29
        RD = 0
      Else
        ' 2月末日以外
        Y = Y + Int(RD / 1461) * 4
        RD = RD - Int(RD / 1461) * 1461
      End If
    Else
      If Abs(RD) < 146097 Then
        ' ±400年未満の場合
        If Int(RD / 36524) = RD / 36524 Then
          ' 2月末日の場合
          Y = Y + RD / 36524 * 100
          M = 2
          D = 28
          RD = 0
        Else
          ' 2月末日以外
          Y = Y + Int(RD / 36524) * 100
          RD = RD - Int(RD / 36524) * 36524
        End If
      Else
        ' ±400年以上の場合
        If Int(RD / 146097) = RD / 146097 Then
          ' 2月末日の場合
          Y = Y + RD / 146097 * 400
          M = 2
          D = 29
          RD = 0
        Else
          ' 2月末日以外
          Y = Y + Int(RD / 146097) * 400
          RD = RD - Int(RD / 146097) * 146097
        End If
      End If
    End If
  End If
End Function

' ----------------------------------------------------------
' 2月末日を第0日としてM月末日までの日数を返す
' M = 2～13
' ----------------------------------------------------------
Function MonthDay(M)
  MonthDay = Int(30.59 * (M - 1)) - 30
End Function
```


