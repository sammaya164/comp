---
title: "日付と修正ユリウス日の変換"
categories:
  - VBScript
tags:
  - VBScript
---

日付から修正ユリウス日を算出するプログラムと、修正ユリウス日から日付を算出するプログラムを一つにまとめた。

とくに修正ユリウス日から日付を算出する関数が、今見るとごちゃごちゃし過ぎていたので作り直した。

ファイル名：GDate2.vbs

```vb
Call Main()

' 入出力インターフェース
'
Function Main()
  Dim ans
  Dim ret

  ' 本日の日付をデフォルト値とする
  ans = Year(Date) & "/" & Month(Date) & "/" & Day(Date)
  ' キャンセルボタンが押されるまで繰り返し
  Do
    ret = InputBox("日付または修正ユリウス日を入力して下さい。", , ans)
    If ret = "" Then
      Exit Do
    ElseIf IsNumeric(ret) Then
    ' 修正ユリウス日が入力された場合は日付を算出して表示する
      ans = Join(GetDate(ret, 3), "/")
      MsgBox "修正ユリウス日=" & ret & vbCr & ans
    ElseIf UBound(Split(ret, "/")) = 2 Then
    ' 日付が入力された場合は修正ユリウス日を算出して表示する
      ans = GetMJD(Split(ret, "/"), 3)
      MsgBox ret & vbCr & "修正ユリウス日=" & ans
    End If
  Loop
End Function

' 日付から修正ユリウス日を算出する
' aDate(年月日を表す配列)
' calender(1～3の整数)
'   1:ユリウス暦
'   2:グレゴリオ暦
'   3:日付に応じて暦を選択
'
Function GetMJD(aDate, calender)
  ' 日付に応じてユリウス暦かグレゴリオ暦を選択
  If calender = 3 Then calender = SelectCalender(aDate) ' 1or2
 
  Dim Y, M, D
  Y = aDate(0)
  M = aDate(1)
  D = aDate(2)
 
  ' 1,2月を前年の13,14月にする
  If M < 3 Then
    Y = Y - 1
    M = M + 12
  End If
 
  ' 修正ユリウス日を計算
  Select Case calender
  Case 1
     GetMJD = -678884 + 365 * Y + MonthDay(M - 1) + D + Int(Y / 4)
  Case 2
     GetMJD = -678882 + 365 * Y + MonthDay(M - 1) + D + Int(Y / 4) - Int(Y / 100) + Int(Y / 400)
  End Select
End Function

' 修正ユリウス日から日付を算出、返り値は[2018,8,6]のような配列
' MJD(修正ユリウス日を表す整数か実数)
' calender(1～3の整数)
'   1:ユリウス暦
'   2:グレゴリオ暦
'   3:MJDに応じて暦を選択
'
Function GetDate(MJD, calender)
  ' MJDに応じてユリウス暦かグレゴリオ暦を選択
  If calender = 3 Then calender = SelectCalender(MJD) ' 1or2

  ' MJDを西暦0年(紀元前1年)2月29日を第0日とした日数rdへ変換
  Dim rd
  Select Case calender
  Case 1 ' ユリウス暦の場合
    rd = MJD + 678884
  Case 2 ' グレゴリオ暦の場合
    rd = MJD + 678882
  End Select
 
  ' 計算に必要なパラメータ(年数)
  Dim aYears
  aYears = Array(400, 100, 4, 1)
 
  ' 計算に必要なパラメータ(日数)
  Dim aDays
  aDays = Array(146097, 36524, 1461, 365, 1)

  ' 計算に必要なパラメータ(暦の制御)
  Dim aSequence
  Select Case calender
  Case 1 ' ユリウス暦の場合
    aSequence = Array(0, 0, 1, 1, 1)
  Case 2 ' グレゴリオ暦の場合
    aSequence = Array(1, 1, 1, 1, 1)
  End Select

  ' int(rd) = Σ(i=0 to 4){b(i)*aDays(i)}となるb(i)を求める
  Dim b(4)
  Dim i
  For i = 0 To 4
    If aSequence(i) = 1 Then
      b(i) = Int(rd / aDays(i))
      rd = rd - b(i) * aDays(i)
    End If
  Next
  ' この時点で 0<=rd <1
 
  ' 年を計算
  Dim Y
  Y = 0
  For i = 0 To 3
    If aSequence(i) = 1 Then
      Y = Y + b(i) * aYears(i)
    End If
  Next
 
  ' 月日を計算
  Dim M, D
  If b(4) = 0 Then
    ' 2月末日の場合
    i = 3
    Do Until b(i) <> 0 Or i = 0
      i = i - 1
    Loop
    M = 2
    Select Case i
    Case 0, 2
      D = 29
    Case 1, 3
      D = 28
    End Select
  Else
    ' 2月末日以外の場合
    M = 3
    Do While MonthDay(M) < b(4)
      M = M + 1
    Loop
    D = b(4) - MonthDay(M - 1)
    If M > 12 Then
      Y = Y + 1
      M = M - 12
    End If
  End If
  GetDate = Array(Y, M, D + rd) ' 小数点以下も日に加える
End Function

' 入力が数値(MJD)の場合、-100840未満ならば1を返し、以降ならば2を返す
' 入力が配列(年月日)の場合、1582年10月15日未満ならば1を返し、以降ならば2を返す
'
Function SelectCalender(val)
  If IsNumeric(val) Then
    If CDbl(val) < -100840 Then
      SelectCalender = 1 ' ユリウス暦を表す
    Else
      SelectCalender = 2 ' グレゴリオ暦を表す
    End If
  End If
  If IsArray(val) Then
    If 10000 * val(0) + 100 * val(1) + val(2) < 15821015 Then
      SelectCalender = 1 ' ユリウス暦を表す
    Else
      SelectCalender = 2 ' グレゴリオ暦を表す
    End If
  End If
End Function

' 2月末日を第0日としてM月末日までの日数を返す
' M = 2～13
'
Function MonthDay(M)
  MonthDay = Int(30.59 * (M - 1)) - 30
End Function
```
