---
title: "HTAでテキストエリアのクリックした行を選択する"

categories:
  - VBScript
tags:
  - VBScript
  - HTA
---

TextAreaは、HTMLやHTAファイルで<TextArea>タグを使うと表示できる。TextAreaに複数行の文字列が入力されているとする。

```
Io
Ganymede
Europa
Callisto
```
  
クリックした行を選択したり、文字列を取得したりする方法を以下に記す。

クリックした箇所を、Document.Selection.CreateRangeメソッドを使ってTextRangeオブジェクトとして取得する。
このTextRangeに対し、MoveStartメソッドを使って行頭まで開始位置を移動させ、MoveEndメソッドを使って行末まで終了位置を移動させる。
こうしてTextRangeを行全体に広げる事が出来る。
あとはSelectメソッドで選択でき、Textプロパティで文字列を取得できる。

上記の下線部の工程での、行頭・行末の判断方法だが、1文字ずつ改行かどうかを調べるといった正攻法がうまくいかなかったので、次のようにした。
TextRangeオブジェクトを1文字ずつ広げるとTextRange.Textは長さが1ずつ増加する。
ただし改行箇所では増加が1にならない。この性質を利用して行頭・行末を判断できる。

TextRangeオブジェクトを行頭・行末へ何文字広げれば良いかを取得するユーザ関数：GetCountToMoveを定義した。
これを利用して、クリックした行全体を選択し文字列を拡大表示する例を下に示す。

ファイル名：Main.hta

```html
<html><head>
  <script type="text/vbscript" src="script.lib">
  </script>
</head><body>
  <textarea name="MyList" rows="5"></textarea>
  <div id="MyDisp" style="font-size:12em;"></div>
</body></html>
```

ファイル名：script.lib

```vb
' ----------------------------------------------------------
' ウィンドウをロードしたとき
' ----------------------------------------------------------
Sub Window_onLoad
  Dim aList
  aList = Array("Io", "Ganymede", "Europa", "Callisto")
  MyList.Value = Join(aList, vbCrLf)
End Sub
' ----------------------------------------------------------
' ウィンドウをアンロードしたとき
' ----------------------------------------------------------
Sub Window_onUnLoad
End Sub
' ----------------------------------------------------------
' テキストエリア(MyList)をクリックしたとき
' ----------------------------------------------------------
Sub MyList_onClick
  MyDisp.InnerHTML = SelectLine()
End Sub
' ----------------------------------------------------------
' SelectLine関数
' クリックした行を選択し文字列を返す
' ----------------------------------------------------------
Function SelectLine()
  Dim range1
  Set range1 = Document.Selection.CreateRange()
  range1.Collapse() ' Length = 0
  Call range1.MoveStart _
      ("Character", GetCountToMove(range1,-1))
  Call range1.MoveEnd _
      ("Character", GetCountToMove(range1, 1))
  range1.select()
  SelectLine = range1.Text
  Set range1 = nothing
End function
' ----------------------------------------------------------
' GetCountToMove関数
' TextRange(range1)の端から行頭or行末までの文字数を返す
' sgn=-1なら行頭、sgn=+1なら行末を探索
' ----------------------------------------------------------
Function GetCountToMove(range1, sgn)
  Dim range2, original, count
  Set range2 = range1.Duplicate() ' copy range1
  original = Len(range2.Text)
  count = 0
  Do while Len(range2.text) = original + count
    Select Case sgn
    Case -1 Call range2.MoveStart("Character", -1)
    Case  1 Call range2.MoveEnd  ("Character",  1)
    End Select
    count = count + 1
  Loop
  GetCountToMove = sgn * (count - 1)
End Function
```



