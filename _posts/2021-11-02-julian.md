---
layout: single
categories: VBScript
title: "暦と修正ユリウス日"
---

入力された日付の「修正ユリウス日」を算出するプログラム。

修正ユリウス日とは、1858年11月17日を第0日としたときの通算の日数。準ユリウス日とも呼ぶ。
英語では、Modified Julian Day、略すとMJD。正負の整数であれば午前0時を表すが、小数部で時刻を表す。
なお時刻は世界時（日本標準時-9時間）で考える。
元々、紀元前4713年1月1日正午（世界時）を第0日とした「ユリウス日」というのがあり、これから2400000.5日を引いたものとして定義された。

西暦1年の前年は紀元前1年であるが、天文学では年数計算を簡便にするた為、紀元前1年を西暦0年、紀元前2年を西暦-1年、・・、紀元前N年を西暦-(N-1)年とする紀年法が使われているそうだ。
下記プログラムもこれに従う。たとえば紀元前4713年は西暦-4712年になる。

現在使われている暦はグレゴリオ暦であるが、かつてはユリウス暦が使われていた。
ユリウス暦では、1年を365.25日とし、単純に4年に1回のうるう年がある。
グレゴリオ暦では、1年を365.2425日とし、4年に1回あるうるう年のうち100年に1回（西暦が100で割り切れる年）は平年とし、ただし400年に1回（西暦が400で割り切れる年）はやはりうるう年となる。ユリウス暦からグレゴリオ暦への切り替えの為1582年10月4日の翌日は10月15日となる。なおグレゴリオ暦の導入時期は国によって異なるので、古文書等の調査には注意を要する。ところで1年（1太陽年）の正確な日数は年々変動しているが、2017年版の『天文年鑑』を参照し、計算すると2017.5年時点で約365.2421893日になるようだ。

ユリウス暦は、紀元前45年にローマで導入されたが、当初は4年に1回とすべきうるう年を、3年に1回で誤運用してしまった。
誤りに気づいた後、調整の為うるう年を設けない期間が設けられ、西暦8年のうるう年以降は4年に1回で運用された。
下記プログラムではユリウス暦の期間（1582年10月4日以前）は単純に4年に1回（西暦が4で割り切れる年に）うるう年を設けている。

ファイル名：MJD.vbs

```vb
Dim Y, M, D

Y = InputBox("年を入力して下さい", , Year(Date))
M = InputBox("月を入力して下さい", , Month(Date))
D = InputBox("日を入力して下さい", , Day(Date))
MsgBox Y & "年" & M & "月" & D & "日" & vbCr & _
         "修正ユリウス日 = " & MJD(Y, M, D)

' ----------------------------------------------------------
' 修正ユリウス日を返す
' 1582年10月15日以降はグレゴリオ暦の日付とみなす
' それより前はユリウス暦の日付とみなす
' ----------------------------------------------------------
Function MJD(ByVal Y, ByVal M, ByVal D)
  Dim P5, JD
  ' 年月日を8桁の数字に変換
  P5 = 10000 * Y + 100 * M + D
  If P5 < 15821015 Then
    JD = Julian(Y, M, D, 1) ' ユリウス暦
  Else
    JD = Julian(Y, M, D, 2) ' グレゴリオ暦
  End If
  ' 結果を返す
  MJD = JD
End Function

' ----------------------------------------------------------
' 修正ユリウス日を算出
' ----------------------------------------------------------
Function Julian(ByVal Y, ByVal M, ByVal D, intCalender)
' intCalender:暦を指定 (1:ユリウス暦, 2:グレゴリオ暦)
  Dim JD

  ' 1,2月を前年の13,14月にする
  If M < 3 Then
    Y = Y - 1
    M = M + 12
  End If
  ' 暦に応じて日付から修正ユリウス日を算出
  Select Case intCalender
  Case 1
    JD = - 678884 + 365 * Y + MonthDay(M - 1) + D + Int(Y / 4)
  Case 2
    JD = - 678882 + 365 * Y + MonthDay(M - 1) + D + Int(Y / 4)_
         - Int(Y / 100) + Int(Y / 400)
  Case Else
  End Select
  ' 結果を返す
  Julian = JD
End Function

' ----------------------------------------------------------
' 2月末日を第0日としてM月末日までの日数を返す
' M = 2～13
' ----------------------------------------------------------
Function MonthDay(M)
  MonthDay = Int(30.59 * (M - 1)) - 30
End Function
```

なおプログラムの作成には、中野主一著『マイコン宇宙講座』（昭和55年）を参考にさせていただいた。
私自身は天文学の知識は無いが、天文学に関する記述が分かりやすかった。BASICからVBSへ直す過程でプログラムは大幅に変えている。
